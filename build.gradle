apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.5'
    compile 'com.graphql-java:graphql-java:2.3.0'
    compile 'org.springframework:spring-core:4.3.4.RELEASE'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.6'

    testCompile 'org.testng:testng:6.10'
}

test {
    useTestNG()
    include "**/*Test.class"
}

publishing {
    publications {
        projectPublish(MavenPublication) {
            from components.java
            groupId 'com.nfl.graphql'
            artifactId 'mediator'
            version PROJECT_VERSION

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description project.description
                    url PROJECT_GITHUB_REPO_URL
                    scm {
                        url PROJECT_GITHUB_REPO_URL
                        connection PROJECT_GITHUB_REPO_URL
                        developerConnection PROJECT_GITHUB_REPO_URL
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url PROJECT_LICENSE_URL
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    from "LICENSE"
    manifest {
        attributes(
                "Specification-Title": 'grahpql-mediator',
                "Specification-Version": PROJECT_VERSION,
                "Implementation-Version": PROJECT_VERSION,
        )
    }
}
